Describe giit#util#vim
  Describe #doautocmd({name}[, {pattern})
    After
      augroup giit_test_util_doautocmd
        autocmd! *
      augroup END
      silent! unlet g:_giit_test_util_doautocmd
    End

    It calls {name} autocmd
      augroup giit_test_util_doautocmd
        autocmd! *
        autocmd BufReadPre * let g:_giit_test_util_doautocmd = 1
      augroup END
      Assert False(exists('g:_giit_test_util_doautocmd'))
      call giit#util#vim#doautocmd('BufReadPre')
      Assert True(g:_giit_test_util_doautocmd)
    End

    It calls {name} autocmd with {pattern}
      augroup giit_test_util_doautocmd
        autocmd! *
        autocmd User giit_test_util_doautocmd let g:_giit_test_util_doautocmd = 1
      augroup END
      Assert False(exists('g:_giit_test_util_doautocmd'))
      call giit#util#vim#doautocmd('User', 'giit_test_util_doautocmd')
      Assert True(g:_giit_test_util_doautocmd)
    End

    It does not show 'No matching autocommands' warning even the {name} autocmd is not registered
      augroup giit_test_util_doautocmd
        autocmd! *
        autocmd BufReadPre foo:* let g:_giit_test_util_doautocmd = 1
      augroup END
      " exists('#BufReadPre') returns 1 while BufReadPre foo:* is registered
      Assert True(exists('#BufReadPre'))
      " But a builtin doautocmd shows 'No matching autocommands' warning while
      " a current buffer does not match with the registered pattern.
      try
        redir => content
        doautocmd BufReadPre
      finally
        redir END
      endtry
      Assert Equals(split(content, "\n"), ['No matching autocommands'])
      " But giit#util#vim#doautocmd does not call autocmd in this case
      try
        redir => content
        call giit#util#vim#doautocmd('BufReadPre')
      finally
        redir END
      endtry
      Assert Equals(split(content, "\n"), [])
      " The registered autocmd should not be called
      Assert False(exists('g:_giit_test_util_doautocmd'))
    End
  End

  Describe #syncbind()
    " TODO
  End
End
